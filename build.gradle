buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: "forge"
apply plugin: "signing"

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc
}

task compile (type: JavaCompile)

task apiJar(type: Jar) {
    classifier = "api"
    from fileTree(dir: "src/main/java").matching {
        include "com/github/haringat/openjscomputers/api/**"
        include "com/github/haringat/openjscomputers/v8/api/DirectApi"
        include "com/github/haringat/openjscomputers/v8/api/ObjectApi"
    }
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from fileTree(dir: "src/main/java")
}

artifacts {
    archives javadocJar, sourcesJar, apiJar
}

signing {
    sign configurations.archives
}

file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = "${config.minecraft.version}-${config.ocjs.version}"
group = "com.github.haringat" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = config.ocjs.modid

minecraft {
    version = config.forge.version
    runDir = "eclipse"

    replace("\${OJSC_VERSION}", project.version)
    replace("\${OJSC_NAME}", config.ocjs.name)
    replace("\${OJSC_MODID}", config.ocjs.modid)
}

configurations {
    embed
    compile.extendsFrom(embed)
}

repositories {
    maven {
        name = "j2v8"
        url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
    }
    maven {
        name = "OpenComputers"
        url = "http://maven.cil.li/"
    }
}

dependencies {
    embed "com.eclipsesource.j2v8:j2v8_macosx_x86_64:${config.j2v8.version}"
    embed "com.eclipsesource.j2v8:j2v8_linux_x86_64:${config.j2v8.version}"
    embed "com.eclipsesource.j2v8:j2v8_win32_x86:${config.j2v8.version}"
    embed "com.eclipsesource.j2v8:j2v8_win32_x86_64:${config.j2v8.version}"
    compile "li.cil.oc:OpenComputers:${config.oc.version}:dev"
    // you may put jars on which you depend on in ./libs
    // or you may define them like so..
    //compile "some.group:artifact:version:classifier"
    //compile "some.group:artifact:version"
      
    // real examples
    //compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
    //compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env

    // for more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

}

/*jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}*/

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
            name "OpenJsComputers"
            packaging "jar"
            // optionally artifactId can be defined here
            description "Adds an ECMAScript (JavaScript) based architecture to OpenComputers."
            url "https://www.github.com/Haringat/JPromise"

            scm {
                connection "scm:git:git://github.com/Haringat/OpenJsComputers.git"
                developerConnection "scm:git:ssh://github.com:Haringat/OpenJsComputers.git"
                url "https://github.com/Haringat/OpenJsComputers"
            }

            licenses {
                license {
                    name "MIT License"
                    url "http://opensource.org/licenses/MIT"
                }
            }

            developers {
                developer {
                    id "Haringat"
                    organizationUrl "https://github.com/Haringat"
                    name "Marcel Mundl"
                    email "Marcel@Mundlhome.de"
                }
            }
        }
    }
}
